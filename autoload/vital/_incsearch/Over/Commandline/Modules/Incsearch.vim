scriptencoding utf-8
let s:save_cpo = &cpo
set cpo&vim


function! s:escape_regex(pattern)
	return substitute(a:pattern, '^\^', '\\_^', "")
endfunction



let s:module = {
\	"name" : "Incsearch",
\	"highlights" : {
\		"incsearch" : "IncSearch"
\	},
\	"pattern" : "",
\	"search_flag" : "",
\	"mode" : "",
\}


function! s:module.search_hl_off()
	if exists("self.search_hl_id")
		call matchdelete(self.search_hl_id)
		unlet self.search_hl_id
	endif
endfunction


function! s:module.search_hl_on(pattern)
	call self.search_hl_off()
	let self.search_hl_id = matchadd(self.highlights.incsearch, a:pattern)
endfunction


function! s:module.on_enter(...)
	let self.old_pos = getpos(".")
endfunction


function! s:module.on_leave(...)
	call setpos(".", self.old_pos)
	call self.search_hl_off()
endfunction


function! s:module.on_char(cmdline)
	call self.search_hl_off()
	let line = a:cmdline.getline()
	let result = get(matchlist(line, self.pattern), 1, "")
	if result != ""
		let pos = searchpos(result, self.search_flag)
		if pos == [0, 0]
			return
		endif
		call self.search_hl_on('\%' . pos[0] . 'l' . (&ignorecase ? '\c' : "") . s:escape_regex(result))
		redraw
	endif
endfunction


function! s:make(...)
	let module = deepcopy(s:module)
	let module.mode   = get(a:, 1, "/")
	let module.pattern = get(a:, 2, '^\(.\+\)')
	let module.search_flag = get(a:, 3, 'c')
	return module
endfunction



let &cpo = s:save_cpo
unlet s:save_cpo
" ___Revitalizer___
" NOTE: below code is generated by :Revitalize.
" Do not mofidify the code nor append new lines
if v:version > 703 || v:version == 703 && has('patch1170')
  function! s:___revitalizer_function___(fstr) abort
    return function(a:fstr)
  endfunction
else
  function! s:___revitalizer_SID() abort
    return matchstr(expand('<sfile>'), '<SNR>\zs\d\+\ze____revitalizer_SID$')
  endfunction
  let s:___revitalizer_sid = '<SNR>' . s:___revitalizer_SID() . '_'
  function! s:___revitalizer_function___(fstr) abort
    return function(substitute(a:fstr, 's:', s:___revitalizer_sid, 'g'))
  endfunction
endif

let s:___revitalizer_functions___ = {'escape_regex': s:___revitalizer_function___('s:escape_regex'),'make': s:___revitalizer_function___('s:make')}

unlet! s:___revitalizer_sid
delfunction s:___revitalizer_function___

function! vital#_incsearch#Over#Commandline#Modules#Incsearch#import() abort
  return s:___revitalizer_functions___
endfunction
" ___Revitalizer___
